BEGIN TRANSACTION
GO
IF EXISTS ( SELECT 1
            FROM   sysobjects 
            WHERE  id = object_id(N'[dbo].[MAKEPARTS]')
                   and OBJECTPROPERTY(id, N'IsProcedure') = 1 )
BEGIN
    DROP PROCEDURE [dbo].[MAKEPARTS]
END
GO

CREATE  procedure [dbo].[MAKEPARTS]
	@LOANID INT,
	@INTERESTAMOUNT INT  
AS 

	DECLARE @PARTMONTH INT,
			@NUMBEROFPARTS INT, 
			@I INT,
			@NEWDATE CHAR(10),
			@PARTAMOUNT bigInt,  
			@PARTSBEGINDATE CHAR(10),
			@PARTINTERESTAMOUNT INT,
			@LOANAMOUNT bigInt,
			@PARTROUND TINYINT,
			@ROUNDFROMBEGIN BIT,
			@PERIOD NVARCHAR(10)

	SELECT @PARTSBEGINDATE=PARTSBEGINDATE,@NEWDATE=PARTSBEGINDATE,@PARTMONTH =PARTMONTHS,  
			@NUMBEROFPARTS=LOAN.PARTNUMBER,@LOANAMOUNT=AMOUNT, @PERIOD = RTRIM(PERIOD)
		FROM   LOAN  
			INNER JOIN LOANTYPE ON LOAN.LOANTYPEID = LOANTYPE.LOANTYPEID  
		WHERE (LOANID =@LOANID)  
  
	IF DBO.IS_VALIDSHAMSIDATE(@PARTSBEGINDATE)=0 RETURN  
  
	EXEC GET_SYSTEMSETTING 'PARTROUND',@PARTROUND OUTPUT  
	EXEC GET_SYSTEMSETTING 'ROUNDFROMBEGIN',@ROUNDFROMBEGIN OUTPUT  
  
	SET @PARTINTERESTAMOUNT = @INTERESTAMOUNT/@NUMBEROFPARTS;  
	SET @PARTAMOUNT = @LOANAMOUNT/@NUMBEROFPARTS;  

	SET @PARTAMOUNT=@PARTAMOUNT-@PARTAMOUNT%POWER(10,@PARTROUND)  
	SET @PARTINTERESTAMOUNT=@PARTINTERESTAMOUNT-@PARTINTERESTAMOUNT%POWER(10,@PARTROUND)  

	SET @I=1  
	
	WHILE @I<=@NUMBEROFPARTS  
	BEGIN  
		INSERT INTO FORCEPAYMENT(PARENTID , FORCETYPEID , AMOUNT , MATUREDATE , ORDERNO , INTERESTAMOUNT )  
			VALUES (@LOANID , 1 , @PartAmount , @NEWDATE , @I , @PARTINTERESTAMOUNT)  
  
		SET @I=@I+1;  
		IF (@PERIOD = 'ماه') OR (@PERIOD = '') 
			SET @NEWDATE = DBO.SHAMSIINCDATE(@NEWDATE , 0 , @PARTMONTH , 0);  
		IF (@PERIOD = 'هفته') 
			SET @NEWDATE = DBO.SHAMSIINCDATE(@NEWDATE , 0 , 0 , @PARTMONTH*7);  
		IF (@PERIOD = 'روز') 
			SET @NEWDATE = DBO.SHAMSIINCDATE(@NEWDATE , 0 , 0 , @PARTMONTH);
	END;  
	  
	UPDATE FORCEPAYMENT   
	SET AMOUNT = AMOUNT + @LOANAMOUNT - @NUMBEROFPARTS * @PartAmount,  
		INTERESTAMOUNT= INTERESTAMOUNT + @INTERESTAMOUNT - @NUMBEROFPARTS * @PARTINTERESTAMOUNT  
	WHERE PARENTID = @LOANID 
		AND FORCETYPEID = 1 
		AND ORDERNO = CASE @ROUNDFROMBEGIN WHEN 0 THEN 1 ELSE @NUMBEROFPARTS END

GO

COMMIT
GO